buildscript {
    dependencies.add 'classpath', 'org.spongepowered:vanillagradle:0.2.1-20240507.024226-82'
}

plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-20240507.024226-82'
    id 'maven-publish'
}

archivesBaseName = "${mod_id}-common"

minecraft {
    version(minecraft_version)
    runs {
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {

            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.3'
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.1.2-beta.6"))
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

processResources {
    filesMatching("pack.mcmeta") {
        expand 'mod_name': project.mod_name
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
